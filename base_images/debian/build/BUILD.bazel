load("@rules_distroless//distroless:defs.bzl", "passwd")
load("@rules_oci//oci:defs.bzl", "oci_push")
load("@rules_pkg//pkg:mappings.bzl", "pkg_mklink")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//base_images/debian:debian12.bzl", "debian_image")
load("//bazel/oci:defs.bzl", "image")

package(default_visibility = ["//visibility:public"])

passwd(
    name = "passwd",
    entries = [
        dict(
            gecos = ["root"],
            gid = 0,
            home = "/root",
            shell = "/usr/bin/bash",
            uid = 0,
            username = "root",
        ),
    ],
)

pkg_mklink(
    name = "usr_bin_python_symlink",
    link_name = "/usr/bin/python",
    target = "/usr/bin/python3.11",
)

pkg_mklink(
    name = "usr_bin_python3_symlink",
    link_name = "/usr/bin/python3",
    target = "/usr/bin/python3.11",
)

pkg_mklink(
    name = "usr_sbin_iptables",
    link_name = "/usr/sbin/iptables",
    target = "/usr/sbin/iptables-legacy",
)

pkg_mklink(
    name = "usr_sbin_ip6tables",
    link_name = "/usr/sbin/ip6tables",
    target = "/usr/sbin/ip6tables-legacy",
)

pkg_tar(
    name = "symlinks_layer",
    srcs = [
        ":usr_bin_python3_symlink",
        ":usr_bin_python_symlink",
        ":usr_sbin_ip6tables",
        ":usr_sbin_iptables",
    ],
)

pkg_tar(
    name = "docker_layer",
    srcs = [
        "@docker//:containerd-bin",
        "@docker//:containerd-shim-runc-v2-bin",
        "@docker//:ctr-bin",
        "@docker//:docker-bin",
        "@docker//:docker-init-bin",
        "@docker//:docker-proxy-bin",
        "@docker//:dockerd-bin",
        "@docker//:runc-bin",
    ],
    package_dir = "/usr/bin",
)

debian_image(
    name = "build_base",
    debian_packages = [
        "@debian12//thin-provisioning-tools",
        "@debian12//fuse-overlayfs",
        "@debian12//iptables",
        "@debian12//libmpfr6",
        "@debian12//libsigsegv2",
        "@debian12//grep",
        "@debian12//git",
        "@debian12//findutils",
        "@debian12//gawk",
        "@debian12//diffutils",
        "@debian12//sed",
        "@debian12//xz-utils",
        "@debian12//liblz4-1",
        "@debian12//libzstd-dev",
        "@debian12//coreutils",
        "@debian12//bash",
        # required to replicate distroless/static
        "@debian12//base-files",
        "@debian12//netbase",
        "@debian12//tzdata",
        "@debian12//ca-certificates",
        # required to replicate distroless/base
        "@debian12//libc6",
        "@debian12//libssl3",
        "@debian12//openssl",
        # required to replicate distroless/cc
        "@debian12//libgcc-s1",
        "@debian12//libgomp1",
        "@debian12//libstdc++6",
        # required to replicate distroless/python
        "@debian12//dash",
        "@debian12//libc-bin",
        "@debian12//python3.11-minimal",
        "@debian12//libpython3.11-stdlib",
        "@debian12//python3-distutils",
    ],
    env = {"LANG": "C.UTF-8"},
)

debian_image(
    name = "build_base_cpp",
    base = ":build_base",
    debian_packages = [
        "@debian12//autotools-dev",
        "@debian12//automake",
        "@debian12//binutils",
        "@debian12//g++",
    ],
)

image(
    name = "build",
    base = "build_base_cpp",
    tars = [
        ":symlinks_layer",
        ":docker_layer",
        ":passwd",
    ],
    visibility = ["//visibility:private"],
)

oci_push(
    name = "push",
    image = ":build",
    remote_tags = ["latest"],
    repository = "index.docker.io/juanzolotoochin/ubuntu-build-v2",
)
